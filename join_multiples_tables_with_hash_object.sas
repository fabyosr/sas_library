DATA TMP_CLIENTE;
INPUT ID_CLI
      NOME $30.;
DATALINES;
1 JOAO
2 MARIA
3 JOSÉ
;
RUN;
DATA TMP_PRODUTOS;
INPUT ID_PROD
      PRODUTO $30.;
DATALINES;
1 BANANA
2 MAÇA
3 LARANJA
;
RUN;
DATA TMP_PEDIDO;
INPUT ID_PEDIDO
      ID_CLIENTE
      ID_PRODUTO
      QTDE
      PRECO_UNITARIO;
DATALINES;
1 1 1 2 3
1 1 2 2 3
1 1 3 2 3
2 2 3 2 3
3 5 2 2 3
4 3 3 2 3
5 5 3 2 3
6 1 1 2 3
;
RUN;
DATA TMP_HT();
 IF 0 THEN SET TMP_CLIENTE; /* CLIENTE HASH TABLE */
 DECLARE HASH CLIENTE (DATASET: 'TMP_CLIENTE'); /* DECLARE THE NAME CLIENTE FOR HASH */
 CLIENTE.DEFINEKEY ('ID_CLI'); /* IDENTIFY FIELDS TO USE AS KEYS */
 CLIENTE.DEFINEDATA ('NOME'); /* IDENTIFY FIELDS TO USE AS DATA */
 CLIENTE.DEFINEDONE (); /* COMPLETE HASH TABLE DEFINITION */

 IF 0 THEN SET TMP_PRODUTOS; /* PRODUTOS HASH TABLE */
 DECLARE HASH PROD (DATASET: 'TMP_PRODUTOS'); /* DECLARE THE NAME CLIENTE FOR HASH */
 PROD.DEFINEKEY ('ID_PROD'); /* IDENTIFY FIELDS TO USE AS KEYS */
 PROD.DEFINEDATA ('PRODUTO'); /* IDENTIFY FIELDS TO USE AS DATA */
 PROD.DEFINEDONE (); /* COMPLETE HASH TABLE DEFINITION */

 DO UNTIL (EOF2) ; /* LOOP TO READ RECORDS FROM PEDIDOS */
    SET TMP_PEDIDO(KEEP=ID_PEDIDO ID_CLIENTE ID_PRODUTO) END = EOF2;
    CALL MISSING(NOME); /* INITIALIZES VARIABLE TO BE COMPLETED  */
    CALL MISSING(PRODUTO); /* INITIALIZES VARIABLE TO BE COMPLETED  */
    FIND1 = CLIENTE.FIND(KEY:ID_CLIENTE); /* LOOKUP EACH ID_CLIENTE IN HASH PLAN */
    FIND2 = PROD.FIND(KEY:ID_PRODUTO); /* LOOKUP EACH ID_PRODUTO IN HASH PLAN */
    OUTPUT;
 END;
 STOP;
 DROP ID_CLI ID_PROD FIND1 FIND2;
RUN;
